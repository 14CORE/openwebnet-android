// TODO
// http://stackoverflow.com/questions/18358297/android-test-code-coverage-with-jacoco-gradle-plugin
// http://jeroenmols.com/blog/2015/11/13/traviscoveralls/

/*
ADD THIS

apply from: '../jacoco.gradle'

android {
    jacoco {
        version '0.7.5.201505241946'
    }
}
*/

apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

// ----- jacoco -----

def coverageSourceDirs = [
    'src/main/java',
    'src/test/java'
]

task jacocoTestReport(type: JacocoReport, dependsOn: "connectedDebugAndroidTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories = fileTree(
        dir: './build/intermediates/classes/debug',
        excludes: [
            '**/R*.class',
            '**/*$InjectAdapter.class',
            '**/*$ModuleAdapter.class',
            '**/*$ViewInjector*.class'
        ])
    sourceDirectories = files(coverageSourceDirs)
    executionData = files("$buildDir/jacoco/testDebug.exec")
    // Bit hacky but fixes https://code.google.com/p/android/issues/detail?id=69174.
    // We iterate through the compiled .class tree and rename $$ to $.
    doFirst {
        new File("$buildDir/intermediates/classes/").eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}

// ----- coveralls -----

coveralls {
    //sourceDirs = files('app/src/main/java').flatten()
    jacocoReportPath = "${buildDir}/reports/coverage/debug/report.xml"
}

tasks.coveralls {
    dependsOn 'connectedAndroidTest'
    onlyIf { System.env.'CI' }
}